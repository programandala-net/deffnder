; fn_lookup16.z80s
;
; A database BASIC function for ZX Spectrum

; Version B-00-20150813

; This file is part of DEFFNder:
; http://programandala.net/en.program.deffnder.html

; --------------------------------------------------------------
; Author and license

; Copyright (C) 2014,2015 Marcos Cruz (programandala.net)

; You may do whatever you want with this work, so long as you
; retain the copyright notice(s) and this license in all
; redistributed copies and derived works. There is no warranty.

; --------------------------------------------------------------
; Description

; This code adds a function to Sinclair BASIC that searchs a
; lookup table of 16-bit values, stored in a string, for a
; 16-bit key value, and returns its associated value.

; --------------------------------------------------------------
; Usage

; A BASIC function must be defined this way:
; (the actual names are unimportant):
;   10 DEF FN f(h$,k)=USR lookup16
; Where:
;   h$       = the haystack, the searched string,
;              with the following structure:
;              every 16-bit key precedes its associated
;              16-bit value; both are stored in two bytes
;              (chars of the string), in the usual Z80 format
;              (least significant byte first).
;   k        = the key to be searched for
;   lookup16 = address of the machine code routine

; Example for a text adventure, where a$ holds a lookup table of
; actions associated to line numbers:
;   GO TO FN f(a$,action)
  
; WARNING: As explained above, the haystack string must consist
; of groups of exactly 4 bytes: a 16-bit key followed by its
; 16-bit value. When the string is not well formed (for example,
; any single character is missing), the end check will fail and
; strange things will happen.

; --------------------------------------------------------------
; Internal

; During the execution of a FN, the system variable DEFADD holds
; the address of the first parameter of its DEF FN definition
; (the first address after the opening paren). The structure of
; DEF FN i(h$,n) is the following:

; offset  content
; ------  -------
; -04     DEF FN
; -03     f(
; +00     h$
; +02     14
; +03     [string type]      
; +04     [string address, LSB first]
; +06     [string length, LSB first]
; +08     ,
; +09     k
; +10     14
; +11     0
; +12     [sign byte: 0=positive; 255=negative]
; +13     [16-bit number, LSB first]
; +15     0
; +16     )=

; --------------------------------------------------------------
; History of this file

; 2015-02-28: First version (started with the code of
; <fn_termn.z80s>).
;
; 2015-03-01: Fix: Usage.
;
; 2015-08-10: Revision. License.
;
; 2015-08-13: Fixed typo in the usage instructions. Thanks Derek
; (http://www.worldofspectrum.org/forums/discussion/comment/831230/#Comment_831230).

; --------------------------------------------------------------

  proc

  ; The code is relocatable

  public fn_lookup16,fn_lookup16_size
fn_lookup16:

  local check
  local next
  local found

; ..............................
; System variables

  local DEFADD
DEFADD equ 23563

; ..............................
; Parameters

  ld ix,(DEFADD)

  ld c,(ix+6)
  ld b,(ix+7)
  ; bc = len of h$
  ld a,b
  or c ; is h$ empty?
  ret z ; if so, back to BASIC (the function returns 0)
  ld l,(ix+4)
  ld h,(ix+5)
  ; hl = start of h$
  ld e,(ix+13)
  ld d,(ix+14)
  ; de = k

; ..............................
check:

  ; Check the current key

  ; hl = address of the current key
  ; de = key searched for

  ld a,(hl)
  cp e
  inc hl
  jr nz,next
  ld a,(hl)
  cp d
  jr z,found

next:

  ; hl = address of the second byte of the current key
  ; bc = remaining length

  inc hl
  inc hl
  inc hl ; hl = address of the next key
  dec bc
  dec bc
  dec bc
  dec bc ; bc = remaining chars
  ld a,b
  or c ; end of the haystack?
  ret z ; if so, back to BASIC (the function returns 0)
  
  jr check

found:

  ; The key was found

  ; hl = address of the second byte of the current key

  inc hl
  ld c,(hl)
  inc hl
  ld b,(hl)
  ; bc =  value
  ret ; back to BASIC (the function returns the value)


fn_lookup16_size: equ $-fn_lookup16

  endp

; vim: textwidth=64
