; fn_instr.z80s
;
; INSTR BASIC function for ZX Spectrum

; Version B-00-20150122

; This file is part of DEFFNder:
; http://programandala.net/en.program.deffnder.html

; --------------------------------------------------------------
; Authors and license

; Copyright (C) 1986 Ricardo Serrial Wigge
;   The code was published in Microhobby magazine,
;   issue 77 (1986-05-06), pages 22-24:
;     http://microhobby.org/
;     http://microhobby.speccy.cz/mhf/077/MH077_22.jpg
;     http://microhobby.speccy.cz/mhf/077/MH077_23.jpg
;     http://microhobby.speccy.cz/mhf/077/MH077_24.jpg

; Copyright (C) 2014 Marcos Cruz (programandala.net)
;   Typed, commented and improved the code.

; You may do whatever you want with this work, so long as you
; retain the copyright notice(s) and this license in all
; redistributed copies and derived works. There is no warranty.

; --------------------------------------------------------------
; Usage

; The routine is called from BASIC using a function defined this way
; (the actual names are unimportant):
;   10 DEF FN i(h$,n$,p)=USR instr
; Where:
;   h$    = the haystack, the searched string
;   n$    = the needle, the string to be searched for
;   p     = start search position in h$
;   instr = address of the machine code routine

; Example:
;   PRINT FN i("En vilagxo de La Mancxo kies nomon mi ne...","Mancxo",1)

; --------------------------------------------------------------
; Internal

; During the execution of a FN, the system variable DEFADD holds
; the address of the first parameter of its DEF FN definition
; (the first address after the opening paren). The structure of
; DEF FN i(h$,n$,p) is the following:

; offset  content
; ------  -------
; -03     DEF FN
; -02     i
; -01     (
; +00     h
; +01     $
; +02     14
; +03     [string type]      
; +04     [string address, LSB first]
; +06     [string length, LSB first]
; +08     ,
; +09     n
; +10     $
; +11     14
; +12     [string type]      
; +13     [string address, LSB first]
; +15     [string length, LSB first]
; +17     ,
; +18     p
; +19     14
; +20     0
; +21     [sign byte: 0=positive; 255=negative]
; +22     [16-bit number, LSB first]
; +24     0
; +25     )
; +26     =

; --------------------------------------------------------------
; History of this file

; 2014-08-06:
;
; Typed from the magazine and compiled with Pasmo.  The code had
; useless NOP and DS.
;
; Problem: The FN function always returns 0, and somehow
; corrupts the STOP command, because it is ignored by the
; interpreter. The BREAK key causes the system to frozen.
;
; Fix: I modified the code: I saved the content of the UNUSED
; system variable, and restored it before returning. The strange
; behaviours ceased. It seems that variable is not so "unused"
; by the system, at least by ZX Spectrum 128K, or 48K with the
; gw03 ROM.
;
; Change: An optional method is provided: the parameter zone of
; the function is used as storage instead of UNUSED. Conditional
; compilation is used.
;
; 2014-08-10:
;
; Removed unnecessary saving and restoring of registers.
;
; New: 'proc', 'public' and 'local', in order to make it
; possible to combine several modules into a single file.
;
; 2015-01-22:
;
; Some typos fixed. Some changes for publication. Version B-00.
;
; 2015-02-27: Improved comment.
;
; 2015-08-10: Revision. License.

; --------------------------------------------------------------
; To-do list

; Check if the start position is greater than the length of the
; haystack.

; --------------------------------------------------------------

  proc

  ; The code is relocatable

  public fn_instr,fn_instr_size
fn_instr:

  local again
  local exit
  local found
  local haystack_exhausted
  local match
  local needle_exhausted
  local not_found

; ..............................
; Options

; Use the system variable UNUSED as a temporary store?
; Otherwise, the address of the length of the h$ argument in DEF
; FN will be used.

  local USE_UNUSED
  USE_UNUSED equ 0

; If USE_UNUSED is true, do save the content of the UNUSED
; system variable? It seems saving it is not required only by
; the original ZX Spectrum 48K without interfaces.  Other models
; or certain interfaces may use this system variable.

  local SAVE_UNUSED
  SAVE_UNUSED equ 1 

; ..............................
; System variables

  local DEFFADD
DEFADD equ 23563
  local UNUSED
UNUSED equ 23728

; ..............................
; Parameters

  ld ix,(DEFADD)
  ld a,(ix+6)
  or (ix+7) ; empty string?
  jr z, not_found

  if USE_UNUSED && SAVE_UNUSED

    ; Save the content of the UNUSED system variable

    ld hl,(UNUSED)
    ld (backup),hl

  endif

  ld e,(ix+4)
  ld d,(ix+5)
  ; de = address of h$
  ld l,(ix+6)
  ld h,(ix+7)
  ; hl = length of h$
  add hl,de
  ; hl = address after the end of h$

  if USE_UNUSED
    ld (UNUSED),hl
  else
    ld (ix+6),l
    ld (ix+7),h
  endif

  ld l,(ix+22)
  ld h,(ix+23)
  ; hl = p
  dec hl
  add hl,de
  ; hl = start address in h$

; ..............................
again:

  ; Start searching h$ for n$ from the current position of h$

  ld e,(ix+13)
  ld d,(ix+14)
  dec de
  ; de = address of n$ -1
  ld c,(ix+15)
  ld b,(ix+16)
  ; bc =length of n$

; ..............................
match:
  
  ; Keep on searching h$

  inc de
  ; de = address of current char of n$
  ; hl = address of current char of h$
  ld a,(de)
  cpi ; compare a with (hl) and increment hl
  ex af,af'
  push hl
  push de

  if USE_UNUSED
    ld de,(UNUSED)
  else
    ld e,(ix+6)
    ld d,(ix+7)
  endif
  ; de = address after the end of h$
  and a
  sbc hl,de
  ; z = last char of h$?
  pop de
  pop hl
  jr z,haystack_exhausted 

  ld a,b
  or c
  ; z = last char of n$?
  jr z,needle_exhausted

  ex af,af'
  ; z = (hl)=(de)?
  jr z,match
  jr again

; ..............................
needle_exhausted:

  ex af,af'
  ; z = (hl)=(de)?
  jr nz,again
  jr found

; ..............................
haystack_exhausted:

  ld a,b
  or c
  ; z = bc=0?
  jr nz,not_found

  ex af,af'
  ; z = (hl)=(de)?
  jr z,found

; ..............................
not_found:

  ld bc,0
  jr exit

; ..............................
found:

  ; hl = address of current char of h$
  ld e,(ix+4)
  ld d,(ix+5)
  ; de = address of h$
  and a
  sbc hl,de
  ; hl = offset to the current char of h$
  ld e,(ix+15)
  ld d,(ix+16)
  ; de = length of n$
  and a
  sbc hl,de
  inc hl
  ; hl = position of n$ in h$
  ld b,h
  ld c,l
  ; bc = position of n$ in h$

; ..............................
exit:

  ; bc = position of n$ in h$ (or zero)

  if USE_UNUSED && SAVE_UNUSED

    backup equ $+1

    ld hl,0
    ld (UNUSED),hl

  endif

  ret

fn_instr_size equ $-fn_instr

  endp

; vim: textwidth=64
